<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>NaturalsAddition</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Background">Chuangjie Xu 2011, with changes by Martin Escardo later.

</a><a id="59" class="Markup">\begin{code}</a>

<a id="73" class="Symbol">{-#</a> <a id="77" class="Keyword">OPTIONS</a> <a id="85" class="Pragma">--without-K</a> <a id="97" class="Pragma">--exact-split</a> <a id="111" class="Pragma">--safe</a> <a id="118" class="Symbol">#-}</a>

<a id="123" class="Keyword">module</a> <a id="130" href="NaturalsAddition.html" class="Module">NaturalsAddition</a> <a id="147" class="Keyword">where</a>

<a id="154" class="Keyword">open</a> <a id="159" class="Keyword">import</a> <a id="166" href="SpartanMLTT.html" class="Module">SpartanMLTT</a> <a id="178" class="Keyword">hiding</a> <a id="185" class="Symbol">(</a><a id="186" href="Plus.html#178" class="Datatype Operator">_+_</a><a id="189" class="Symbol">)</a>

<a id="192" class="Keyword">infixl</a> <a id="199" class="Number">31</a> <a id="202" href="NaturalsAddition.html#207" class="Function Operator">_+_</a>

<a id="_+_"></a><a id="207" href="NaturalsAddition.html#207" class="Function Operator">_+_</a> <a id="211" class="Symbol">:</a> <a id="213" href="Natural-Numbers-Type.html#105" class="Datatype">ℕ</a> <a id="215" class="Symbol">→</a> <a id="217" href="Natural-Numbers-Type.html#105" class="Datatype">ℕ</a> <a id="219" class="Symbol">→</a> <a id="221" href="Natural-Numbers-Type.html#105" class="Datatype">ℕ</a>
<a id="223" href="NaturalsAddition.html#223" class="Bound">n</a> <a id="225" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="227" class="Number">0</a> <a id="229" class="Symbol">=</a> <a id="231" href="NaturalsAddition.html#223" class="Bound">n</a>
<a id="233" href="NaturalsAddition.html#233" class="Bound">n</a> <a id="235" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="237" class="Symbol">(</a><a id="238" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="243" href="NaturalsAddition.html#243" class="Bound">m</a><a id="244" class="Symbol">)</a> <a id="246" class="Symbol">=</a> <a id="248" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a><a id="252" class="Symbol">(</a><a id="253" href="NaturalsAddition.html#233" class="Bound">n</a> <a id="255" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="257" href="NaturalsAddition.html#243" class="Bound">m</a><a id="258" class="Symbol">)</a>

<a id="zero-right-neutral"></a><a id="261" href="NaturalsAddition.html#261" class="Function">zero-right-neutral</a> <a id="280" class="Symbol">:</a> <a id="282" class="Symbol">(</a><a id="283" href="NaturalsAddition.html#283" class="Bound">n</a> <a id="285" class="Symbol">:</a> <a id="287" href="Natural-Numbers-Type.html#105" class="Datatype">ℕ</a><a id="288" class="Symbol">)</a> <a id="290" class="Symbol">→</a> <a id="292" href="NaturalsAddition.html#283" class="Bound">n</a> <a id="294" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="296" class="Number">0</a> <a id="298" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="300" href="NaturalsAddition.html#283" class="Bound">n</a>
<a id="302" href="NaturalsAddition.html#261" class="Function">zero-right-neutral</a> <a id="321" href="NaturalsAddition.html#321" class="Bound">n</a> <a id="323" class="Symbol">=</a> <a id="325" href="Identity-Type.html#162" class="InductiveConstructor">refl</a>

<a id="zero-left-neutral"></a><a id="331" href="NaturalsAddition.html#331" class="Function">zero-left-neutral</a> <a id="349" class="Symbol">:</a> <a id="351" class="Symbol">(</a><a id="352" href="NaturalsAddition.html#352" class="Bound">n</a> <a id="354" class="Symbol">:</a> <a id="356" href="Natural-Numbers-Type.html#105" class="Datatype">ℕ</a><a id="357" class="Symbol">)</a> <a id="359" class="Symbol">→</a> <a id="361" class="Number">0</a> <a id="363" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="365" href="NaturalsAddition.html#352" class="Bound">n</a> <a id="367" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="369" href="NaturalsAddition.html#352" class="Bound">n</a>
<a id="371" href="NaturalsAddition.html#331" class="Function">zero-left-neutral</a> <a id="389" class="Symbol">=</a> <a id="391" href="NaturalNumbers.html#329" class="Function">induction</a> <a id="401" href="NaturalsAddition.html#422" class="Function">base</a> <a id="406" href="NaturalsAddition.html#458" class="Function">step</a>
  <a id="413" class="Keyword">where</a>
   <a id="422" href="NaturalsAddition.html#422" class="Function">base</a> <a id="427" class="Symbol">:</a> <a id="429" class="Number">0</a> <a id="431" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="433" class="Number">0</a> <a id="435" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="437" class="Number">0</a>
   <a id="442" href="NaturalsAddition.html#422" class="Function">base</a> <a id="447" class="Symbol">=</a> <a id="449" href="Identity-Type.html#162" class="InductiveConstructor">refl</a>

   <a id="458" href="NaturalsAddition.html#458" class="Function">step</a> <a id="463" class="Symbol">:</a> <a id="465" class="Symbol">(</a><a id="466" href="NaturalsAddition.html#466" class="Bound">n</a> <a id="468" class="Symbol">:</a> <a id="470" href="Natural-Numbers-Type.html#105" class="Datatype">ℕ</a><a id="471" class="Symbol">)</a> <a id="473" class="Symbol">→</a> <a id="475" class="Number">0</a> <a id="477" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="479" href="NaturalsAddition.html#466" class="Bound">n</a> <a id="481" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="483" href="NaturalsAddition.html#466" class="Bound">n</a> <a id="485" class="Symbol">→</a> <a id="487" class="Number">0</a> <a id="489" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="491" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="496" href="NaturalsAddition.html#466" class="Bound">n</a> <a id="498" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="500" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="505" href="NaturalsAddition.html#466" class="Bound">n</a>
   <a id="510" href="NaturalsAddition.html#458" class="Function">step</a> <a id="515" href="NaturalsAddition.html#515" class="Bound">n</a> <a id="517" href="NaturalsAddition.html#517" class="Bound">IH</a> <a id="520" class="Symbol">=</a> <a id="522" class="Number">0</a> <a id="524" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="526" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="531" href="NaturalsAddition.html#515" class="Bound">n</a>   <a id="535" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="538" href="Identity-Type.html#162" class="InductiveConstructor">refl</a> <a id="543" href="Id.html#1743" class="Function Operator">⟩</a>
               <a id="560" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="565" class="Symbol">(</a><a id="566" class="Number">0</a> <a id="568" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="570" href="NaturalsAddition.html#515" class="Bound">n</a><a id="571" class="Symbol">)</a> <a id="573" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="576" href="Id.html#1330" class="Function">ap</a> <a id="579" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="584" href="NaturalsAddition.html#517" class="Bound">IH</a> <a id="587" href="Id.html#1743" class="Function Operator">⟩</a>
               <a id="604" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="609" href="NaturalsAddition.html#515" class="Bound">n</a>       <a id="617" href="Id.html#1825" class="Function Operator">∎</a>

<a id="addition-associativity"></a><a id="620" href="NaturalsAddition.html#620" class="Function">addition-associativity</a> <a id="643" class="Symbol">:</a> <a id="645" class="Symbol">(</a><a id="646" href="NaturalsAddition.html#646" class="Bound">l</a> <a id="648" href="NaturalsAddition.html#648" class="Bound">n</a> <a id="650" href="NaturalsAddition.html#650" class="Bound">m</a> <a id="652" class="Symbol">:</a> <a id="654" href="Natural-Numbers-Type.html#105" class="Datatype">ℕ</a><a id="655" class="Symbol">)</a> <a id="657" class="Symbol">→</a> <a id="659" class="Symbol">(</a><a id="660" href="NaturalsAddition.html#646" class="Bound">l</a> <a id="662" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="664" href="NaturalsAddition.html#648" class="Bound">n</a><a id="665" class="Symbol">)</a> <a id="667" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="669" href="NaturalsAddition.html#650" class="Bound">m</a> <a id="671" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="673" href="NaturalsAddition.html#646" class="Bound">l</a> <a id="675" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="677" class="Symbol">(</a><a id="678" href="NaturalsAddition.html#648" class="Bound">n</a> <a id="680" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="682" href="NaturalsAddition.html#650" class="Bound">m</a><a id="683" class="Symbol">)</a>
<a id="685" href="NaturalsAddition.html#620" class="Function">addition-associativity</a> <a id="708" href="NaturalsAddition.html#708" class="Bound">l</a> <a id="710" href="NaturalsAddition.html#710" class="Bound">n</a> <a id="712" class="Symbol">=</a> <a id="714" href="NaturalNumbers.html#329" class="Function">induction</a> <a id="724" href="NaturalsAddition.html#745" class="Function">base</a> <a id="729" href="NaturalsAddition.html#873" class="Function">step</a>
  <a id="736" class="Keyword">where</a>
   <a id="745" href="NaturalsAddition.html#745" class="Function">base</a> <a id="750" class="Symbol">:</a> <a id="752" class="Symbol">(</a><a id="753" href="NaturalsAddition.html#708" class="Bound">l</a> <a id="755" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="757" href="NaturalsAddition.html#710" class="Bound">n</a><a id="758" class="Symbol">)</a> <a id="760" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="762" class="Number">0</a> <a id="764" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="766" href="NaturalsAddition.html#708" class="Bound">l</a> <a id="768" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="770" class="Symbol">(</a><a id="771" href="NaturalsAddition.html#710" class="Bound">n</a> <a id="773" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="775" class="Number">0</a><a id="776" class="Symbol">)</a>
   <a id="781" href="NaturalsAddition.html#745" class="Function">base</a> <a id="786" class="Symbol">=</a> <a id="788" class="Symbol">(</a><a id="789" href="NaturalsAddition.html#708" class="Bound">l</a> <a id="791" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="793" href="NaturalsAddition.html#710" class="Bound">n</a><a id="794" class="Symbol">)</a> <a id="796" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="798" class="Number">0</a>  <a id="801" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="804" href="Identity-Type.html#162" class="InductiveConstructor">refl</a> <a id="809" href="Id.html#1743" class="Function Operator">⟩</a>
           <a id="822" href="NaturalsAddition.html#708" class="Bound">l</a> <a id="824" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="826" href="NaturalsAddition.html#710" class="Bound">n</a>       <a id="834" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="837" href="Identity-Type.html#162" class="InductiveConstructor">refl</a> <a id="842" href="Id.html#1743" class="Function Operator">⟩</a>
           <a id="855" href="NaturalsAddition.html#708" class="Bound">l</a> <a id="857" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="859" class="Symbol">(</a><a id="860" href="NaturalsAddition.html#710" class="Bound">n</a> <a id="862" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="864" class="Number">0</a><a id="865" class="Symbol">)</a> <a id="867" href="Id.html#1825" class="Function Operator">∎</a>

   <a id="873" href="NaturalsAddition.html#873" class="Function">step</a> <a id="878" class="Symbol">:</a> <a id="880" class="Symbol">(</a><a id="881" href="NaturalsAddition.html#881" class="Bound">m</a> <a id="883" class="Symbol">:</a> <a id="885" href="Natural-Numbers-Type.html#105" class="Datatype">ℕ</a><a id="886" class="Symbol">)</a> <a id="888" class="Symbol">→</a> <a id="890" class="Symbol">(</a><a id="891" href="NaturalsAddition.html#708" class="Bound">l</a> <a id="893" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="895" href="NaturalsAddition.html#710" class="Bound">n</a><a id="896" class="Symbol">)</a> <a id="898" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="900" href="NaturalsAddition.html#881" class="Bound">m</a> <a id="902" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="904" href="NaturalsAddition.html#708" class="Bound">l</a> <a id="906" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="908" class="Symbol">(</a><a id="909" href="NaturalsAddition.html#710" class="Bound">n</a> <a id="911" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="913" href="NaturalsAddition.html#881" class="Bound">m</a><a id="914" class="Symbol">)</a>
                  <a id="934" class="Symbol">→</a> <a id="936" class="Symbol">(</a><a id="937" href="NaturalsAddition.html#708" class="Bound">l</a> <a id="939" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="941" href="NaturalsAddition.html#710" class="Bound">n</a><a id="942" class="Symbol">)</a> <a id="944" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="946" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="951" href="NaturalsAddition.html#881" class="Bound">m</a> <a id="953" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="955" href="NaturalsAddition.html#708" class="Bound">l</a> <a id="957" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="959" class="Symbol">(</a><a id="960" href="NaturalsAddition.html#710" class="Bound">n</a> <a id="962" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="964" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="969" href="NaturalsAddition.html#881" class="Bound">m</a><a id="970" class="Symbol">)</a>
   <a id="975" href="NaturalsAddition.html#873" class="Function">step</a> <a id="980" href="NaturalsAddition.html#980" class="Bound">m</a> <a id="982" href="NaturalsAddition.html#982" class="Bound">IH</a> <a id="985" class="Symbol">=</a> <a id="987" class="Symbol">(</a><a id="988" href="NaturalsAddition.html#708" class="Bound">l</a> <a id="990" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="992" href="NaturalsAddition.html#710" class="Bound">n</a><a id="993" class="Symbol">)</a> <a id="995" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="997" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1002" href="NaturalsAddition.html#980" class="Bound">m</a>   <a id="1006" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="1009" href="Identity-Type.html#162" class="InductiveConstructor">refl</a> <a id="1014" href="Id.html#1743" class="Function Operator">⟩</a>
               <a id="1031" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1036" class="Symbol">((</a><a id="1038" href="NaturalsAddition.html#708" class="Bound">l</a> <a id="1040" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1042" href="NaturalsAddition.html#710" class="Bound">n</a><a id="1043" class="Symbol">)</a> <a id="1045" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1047" href="NaturalsAddition.html#980" class="Bound">m</a><a id="1048" class="Symbol">)</a> <a id="1050" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="1053" href="Id.html#1330" class="Function">ap</a> <a id="1056" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1061" href="NaturalsAddition.html#982" class="Bound">IH</a> <a id="1064" href="Id.html#1743" class="Function Operator">⟩</a>
               <a id="1081" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1086" class="Symbol">(</a><a id="1087" href="NaturalsAddition.html#708" class="Bound">l</a> <a id="1089" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1091" class="Symbol">(</a><a id="1092" href="NaturalsAddition.html#710" class="Bound">n</a> <a id="1094" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1096" href="NaturalsAddition.html#980" class="Bound">m</a><a id="1097" class="Symbol">))</a> <a id="1100" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="1103" href="Identity-Type.html#162" class="InductiveConstructor">refl</a> <a id="1108" href="Id.html#1743" class="Function Operator">⟩</a>
               <a id="1125" href="NaturalsAddition.html#708" class="Bound">l</a> <a id="1127" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1129" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1134" class="Symbol">(</a><a id="1135" href="NaturalsAddition.html#710" class="Bound">n</a> <a id="1137" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1139" href="NaturalsAddition.html#980" class="Bound">m</a><a id="1140" class="Symbol">)</a>   <a id="1144" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="1147" href="Identity-Type.html#162" class="InductiveConstructor">refl</a> <a id="1152" href="Id.html#1743" class="Function Operator">⟩</a>
               <a id="1169" href="NaturalsAddition.html#708" class="Bound">l</a> <a id="1171" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1173" class="Symbol">(</a><a id="1174" href="NaturalsAddition.html#710" class="Bound">n</a> <a id="1176" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1178" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1183" href="NaturalsAddition.html#980" class="Bound">m</a><a id="1184" class="Symbol">)</a>   <a id="1188" href="Id.html#1825" class="Function Operator">∎</a>

<a id="addition-commutativity"></a><a id="1191" href="NaturalsAddition.html#1191" class="Function">addition-commutativity</a> <a id="1214" class="Symbol">:</a> <a id="1216" class="Symbol">(</a><a id="1217" href="NaturalsAddition.html#1217" class="Bound">n</a> <a id="1219" href="NaturalsAddition.html#1219" class="Bound">m</a> <a id="1221" class="Symbol">:</a> <a id="1223" href="Natural-Numbers-Type.html#105" class="Datatype">ℕ</a><a id="1224" class="Symbol">)</a> <a id="1226" class="Symbol">→</a> <a id="1228" href="NaturalsAddition.html#1217" class="Bound">n</a> <a id="1230" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1232" href="NaturalsAddition.html#1219" class="Bound">m</a> <a id="1234" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="1236" href="NaturalsAddition.html#1219" class="Bound">m</a> <a id="1238" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1240" href="NaturalsAddition.html#1217" class="Bound">n</a>
<a id="1242" href="NaturalsAddition.html#1191" class="Function">addition-commutativity</a> <a id="1265" href="NaturalsAddition.html#1265" class="Bound">n</a> <a id="1267" class="Symbol">=</a> <a id="1269" href="NaturalNumbers.html#329" class="Function">induction</a> <a id="1279" href="NaturalsAddition.html#1300" class="Function">base</a> <a id="1284" href="NaturalsAddition.html#1430" class="Function">step</a>
  <a id="1291" class="Keyword">where</a>
   <a id="1300" href="NaturalsAddition.html#1300" class="Function">base</a> <a id="1305" class="Symbol">:</a> <a id="1307" href="NaturalsAddition.html#1265" class="Bound">n</a> <a id="1309" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1311" class="Number">0</a> <a id="1313" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="1315" class="Number">0</a> <a id="1317" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1319" href="NaturalsAddition.html#1265" class="Bound">n</a>
   <a id="1324" href="NaturalsAddition.html#1300" class="Function">base</a> <a id="1329" class="Symbol">=</a> <a id="1331" href="NaturalsAddition.html#1265" class="Bound">n</a> <a id="1333" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1335" class="Number">0</a> <a id="1337" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="1340" href="NaturalsAddition.html#261" class="Function">zero-right-neutral</a> <a id="1359" href="NaturalsAddition.html#1265" class="Bound">n</a> <a id="1361" href="Id.html#1743" class="Function Operator">⟩</a>
          <a id="1373" href="NaturalsAddition.html#1265" class="Bound">n</a>     <a id="1379" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="1382" class="Symbol">(</a><a id="1383" href="NaturalsAddition.html#331" class="Function">zero-left-neutral</a> <a id="1401" href="NaturalsAddition.html#1265" class="Bound">n</a><a id="1402" class="Symbol">)</a><a id="1403" href="Id.html#1249" class="Function Operator">⁻¹</a> <a id="1406" href="Id.html#1743" class="Function Operator">⟩</a>
          <a id="1418" class="Number">0</a> <a id="1420" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1422" href="NaturalsAddition.html#1265" class="Bound">n</a> <a id="1424" href="Id.html#1825" class="Function Operator">∎</a>

   <a id="1430" href="NaturalsAddition.html#1430" class="Function">step</a> <a id="1435" class="Symbol">:</a> <a id="1437" class="Symbol">(</a><a id="1438" href="NaturalsAddition.html#1438" class="Bound">m</a> <a id="1440" class="Symbol">:</a> <a id="1442" href="Natural-Numbers-Type.html#105" class="Datatype">ℕ</a><a id="1443" class="Symbol">)</a> <a id="1445" class="Symbol">→</a> <a id="1447" href="NaturalsAddition.html#1265" class="Bound">n</a> <a id="1449" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1451" href="NaturalsAddition.html#1438" class="Bound">m</a> <a id="1453" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="1455" href="NaturalsAddition.html#1438" class="Bound">m</a> <a id="1457" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1459" href="NaturalsAddition.html#1265" class="Bound">n</a> <a id="1461" class="Symbol">→</a> <a id="1463" href="NaturalsAddition.html#1265" class="Bound">n</a> <a id="1465" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1467" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1472" href="NaturalsAddition.html#1438" class="Bound">m</a> <a id="1474" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="1476" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1481" href="NaturalsAddition.html#1438" class="Bound">m</a> <a id="1483" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1485" href="NaturalsAddition.html#1265" class="Bound">n</a>
   <a id="1490" href="NaturalsAddition.html#1430" class="Function">step</a> <a id="1495" href="NaturalsAddition.html#1495" class="Bound">m</a> <a id="1497" href="NaturalsAddition.html#1497" class="Bound">IH</a> <a id="1500" class="Symbol">=</a> <a id="1502" href="NaturalsAddition.html#1265" class="Bound">n</a> <a id="1504" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1506" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1511" href="NaturalsAddition.html#1495" class="Bound">m</a>   <a id="1515" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="1518" href="Identity-Type.html#162" class="InductiveConstructor">refl</a> <a id="1523" href="Id.html#1743" class="Function Operator">⟩</a>
               <a id="1540" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1545" class="Symbol">(</a><a id="1546" href="NaturalsAddition.html#1265" class="Bound">n</a> <a id="1548" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1550" href="NaturalsAddition.html#1495" class="Bound">m</a><a id="1551" class="Symbol">)</a> <a id="1553" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="1556" href="Id.html#1330" class="Function">ap</a> <a id="1559" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1564" href="NaturalsAddition.html#1497" class="Bound">IH</a> <a id="1567" href="Id.html#1743" class="Function Operator">⟩</a>
               <a id="1584" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1589" class="Symbol">(</a><a id="1590" href="NaturalsAddition.html#1495" class="Bound">m</a> <a id="1592" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1594" href="NaturalsAddition.html#1265" class="Bound">n</a><a id="1595" class="Symbol">)</a> <a id="1597" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="1600" href="NaturalsAddition.html#1788" class="Function">lemma₀</a> <a id="1607" class="Symbol">(</a><a id="1608" href="NaturalsAddition.html#1495" class="Bound">m</a> <a id="1610" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1612" href="NaturalsAddition.html#1265" class="Bound">n</a><a id="1613" class="Symbol">)</a> <a id="1615" href="Id.html#1743" class="Function Operator">⟩</a>
               <a id="1632" class="Number">1</a> <a id="1634" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1636" class="Symbol">(</a><a id="1637" href="NaturalsAddition.html#1495" class="Bound">m</a> <a id="1639" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1641" href="NaturalsAddition.html#1265" class="Bound">n</a><a id="1642" class="Symbol">)</a>  <a id="1645" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="1648" class="Symbol">(</a><a id="1649" href="NaturalsAddition.html#620" class="Function">addition-associativity</a> <a id="1672" class="Number">1</a> <a id="1674" href="NaturalsAddition.html#1495" class="Bound">m</a> <a id="1676" href="NaturalsAddition.html#1265" class="Bound">n</a><a id="1677" class="Symbol">)</a><a id="1678" href="Id.html#1249" class="Function Operator">⁻¹</a> <a id="1681" href="Id.html#1743" class="Function Operator">⟩</a>
               <a id="1698" class="Symbol">(</a><a id="1699" class="Number">1</a> <a id="1701" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1703" href="NaturalsAddition.html#1495" class="Bound">m</a><a id="1704" class="Symbol">)</a> <a id="1706" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1708" href="NaturalsAddition.html#1265" class="Bound">n</a>  <a id="1711" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="1714" href="Id.html#1330" class="Function">ap</a> <a id="1717" class="Symbol">(</a><a id="1718" href="NaturalsAddition.html#207" class="Function Operator">_+</a> <a id="1721" href="NaturalsAddition.html#1265" class="Bound">n</a><a id="1722" class="Symbol">)</a> <a id="1724" class="Symbol">((</a><a id="1726" href="NaturalsAddition.html#1788" class="Function">lemma₀</a> <a id="1733" href="NaturalsAddition.html#1495" class="Bound">m</a><a id="1734" class="Symbol">)</a><a id="1735" href="Id.html#1249" class="Function Operator">⁻¹</a><a id="1737" class="Symbol">)</a> <a id="1739" href="Id.html#1743" class="Function Operator">⟩</a>
               <a id="1756" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1761" href="NaturalsAddition.html#1495" class="Bound">m</a> <a id="1763" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1765" href="NaturalsAddition.html#1265" class="Bound">n</a>   <a id="1769" href="Id.html#1825" class="Function Operator">∎</a>
     <a id="1776" class="Keyword">where</a>
      <a id="1788" href="NaturalsAddition.html#1788" class="Function">lemma₀</a> <a id="1795" class="Symbol">:</a> <a id="1797" class="Symbol">(</a><a id="1798" href="NaturalsAddition.html#1798" class="Bound">k</a> <a id="1800" class="Symbol">:</a> <a id="1802" href="Natural-Numbers-Type.html#105" class="Datatype">ℕ</a><a id="1803" class="Symbol">)</a> <a id="1805" class="Symbol">→</a> <a id="1807" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1812" href="NaturalsAddition.html#1798" class="Bound">k</a> <a id="1814" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="1816" class="Number">1</a> <a id="1818" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1820" href="NaturalsAddition.html#1798" class="Bound">k</a>
      <a id="1828" href="NaturalsAddition.html#1788" class="Function">lemma₀</a> <a id="1835" class="Symbol">=</a> <a id="1837" href="NaturalNumbers.html#329" class="Function">induction</a> <a id="1847" href="NaturalsAddition.html#1882" class="Function">base₀</a> <a id="1853" href="NaturalsAddition.html#1937" class="Function">step₀</a>
        <a id="1867" class="Keyword">where</a>
         <a id="1882" href="NaturalsAddition.html#1882" class="Function">base₀</a> <a id="1888" class="Symbol">:</a> <a id="1890" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1895" class="Number">0</a> <a id="1897" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="1899" class="Number">1</a> <a id="1901" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1903" class="Number">0</a>
         <a id="1914" href="NaturalsAddition.html#1882" class="Function">base₀</a> <a id="1920" class="Symbol">=</a> <a id="1922" href="Identity-Type.html#162" class="InductiveConstructor">refl</a>

         <a id="1937" href="NaturalsAddition.html#1937" class="Function">step₀</a> <a id="1943" class="Symbol">:</a> <a id="1945" class="Symbol">(</a><a id="1946" href="NaturalsAddition.html#1946" class="Bound">k</a> <a id="1948" class="Symbol">:</a> <a id="1950" href="Natural-Numbers-Type.html#105" class="Datatype">ℕ</a><a id="1951" class="Symbol">)</a> <a id="1953" class="Symbol">→</a> <a id="1955" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1960" href="NaturalsAddition.html#1946" class="Bound">k</a> <a id="1962" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="1964" class="Number">1</a> <a id="1966" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1968" href="NaturalsAddition.html#1946" class="Bound">k</a> <a id="1970" class="Symbol">→</a> <a id="1972" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1977" class="Symbol">(</a><a id="1978" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1983" href="NaturalsAddition.html#1946" class="Bound">k</a><a id="1984" class="Symbol">)</a> <a id="1986" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="1988" class="Number">1</a> <a id="1990" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="1992" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="1997" href="NaturalsAddition.html#1946" class="Bound">k</a>
         <a id="2008" href="NaturalsAddition.html#1937" class="Function">step₀</a> <a id="2014" href="NaturalsAddition.html#2014" class="Bound">k</a> <a id="2016" href="NaturalsAddition.html#2016" class="Bound">IH</a> <a id="2019" class="Symbol">=</a> <a id="2021" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="2026" class="Symbol">(</a><a id="2027" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="2032" href="NaturalsAddition.html#2014" class="Bound">k</a><a id="2033" class="Symbol">)</a> <a id="2035" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="2038" href="Id.html#1330" class="Function">ap</a> <a id="2041" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="2046" href="NaturalsAddition.html#2016" class="Bound">IH</a> <a id="2049" href="Id.html#1743" class="Function Operator">⟩</a>
                      <a id="2073" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="2078" class="Symbol">(</a><a id="2079" class="Number">1</a> <a id="2081" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="2083" href="NaturalsAddition.html#2014" class="Bound">k</a><a id="2084" class="Symbol">)</a>  <a id="2087" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="2090" href="Identity-Type.html#162" class="InductiveConstructor">refl</a> <a id="2095" href="Id.html#1743" class="Function Operator">⟩</a>
                      <a id="2119" class="Number">1</a> <a id="2121" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="2123" href="Natural-Numbers-Type.html#132" class="InductiveConstructor">succ</a> <a id="2128" href="NaturalsAddition.html#2014" class="Bound">k</a>    <a id="2133" href="Id.html#1825" class="Function Operator">∎</a>

<a id="trivial-addition-rearrangement"></a><a id="2136" href="NaturalsAddition.html#2136" class="Function">trivial-addition-rearrangement</a> <a id="2167" class="Symbol">:</a> <a id="2169" class="Symbol">(</a><a id="2170" href="NaturalsAddition.html#2170" class="Bound">x</a> <a id="2172" href="NaturalsAddition.html#2172" class="Bound">y</a> <a id="2174" href="NaturalsAddition.html#2174" class="Bound">z</a> <a id="2176" class="Symbol">:</a> <a id="2178" href="Natural-Numbers-Type.html#105" class="Datatype">ℕ</a><a id="2179" class="Symbol">)</a> <a id="2181" class="Symbol">→</a> <a id="2183" href="NaturalsAddition.html#2170" class="Bound">x</a> <a id="2185" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="2187" href="NaturalsAddition.html#2172" class="Bound">y</a> <a id="2189" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="2191" href="NaturalsAddition.html#2174" class="Bound">z</a> <a id="2193" href="Identity-Type.html#121" class="Datatype Operator">≡</a> <a id="2195" href="NaturalsAddition.html#2170" class="Bound">x</a> <a id="2197" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="2199" href="NaturalsAddition.html#2174" class="Bound">z</a> <a id="2201" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="2203" href="NaturalsAddition.html#2172" class="Bound">y</a>
<a id="2205" href="NaturalsAddition.html#2136" class="Function">trivial-addition-rearrangement</a> <a id="2236" href="NaturalsAddition.html#2236" class="Bound">x</a> <a id="2238" href="NaturalsAddition.html#2238" class="Bound">y</a> <a id="2240" href="NaturalsAddition.html#2240" class="Bound">z</a> <a id="2242" class="Symbol">=</a>
  <a id="2246" class="Symbol">(</a><a id="2247" href="NaturalsAddition.html#2236" class="Bound">x</a> <a id="2249" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="2251" href="NaturalsAddition.html#2238" class="Bound">y</a><a id="2252" class="Symbol">)</a> <a id="2254" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="2256" href="NaturalsAddition.html#2240" class="Bound">z</a> <a id="2258" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="2261" href="NaturalsAddition.html#620" class="Function">addition-associativity</a> <a id="2284" href="NaturalsAddition.html#2236" class="Bound">x</a> <a id="2286" href="NaturalsAddition.html#2238" class="Bound">y</a> <a id="2288" href="NaturalsAddition.html#2240" class="Bound">z</a> <a id="2290" href="Id.html#1743" class="Function Operator">⟩</a>
  <a id="2294" href="NaturalsAddition.html#2236" class="Bound">x</a> <a id="2296" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="2298" class="Symbol">(</a><a id="2299" href="NaturalsAddition.html#2238" class="Bound">y</a> <a id="2301" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="2303" href="NaturalsAddition.html#2240" class="Bound">z</a><a id="2304" class="Symbol">)</a> <a id="2306" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="2309" href="Id.html#1330" class="Function">ap</a> <a id="2312" class="Symbol">(</a><a id="2313" href="NaturalsAddition.html#2236" class="Bound">x</a> <a id="2315" href="NaturalsAddition.html#207" class="Function Operator">+_</a><a id="2317" class="Symbol">)</a> <a id="2319" class="Symbol">(</a><a id="2320" href="NaturalsAddition.html#1191" class="Function">addition-commutativity</a> <a id="2343" href="NaturalsAddition.html#2238" class="Bound">y</a> <a id="2345" href="NaturalsAddition.html#2240" class="Bound">z</a><a id="2346" class="Symbol">)</a> <a id="2348" href="Id.html#1743" class="Function Operator">⟩</a>
  <a id="2352" href="NaturalsAddition.html#2236" class="Bound">x</a> <a id="2354" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="2356" class="Symbol">(</a><a id="2357" href="NaturalsAddition.html#2240" class="Bound">z</a> <a id="2359" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="2361" href="NaturalsAddition.html#2238" class="Bound">y</a><a id="2362" class="Symbol">)</a> <a id="2364" href="Id.html#1743" class="Function Operator">≡⟨</a> <a id="2367" class="Symbol">(</a><a id="2368" href="NaturalsAddition.html#620" class="Function">addition-associativity</a> <a id="2391" href="NaturalsAddition.html#2236" class="Bound">x</a> <a id="2393" href="NaturalsAddition.html#2240" class="Bound">z</a> <a id="2395" href="NaturalsAddition.html#2238" class="Bound">y</a><a id="2396" class="Symbol">)</a><a id="2397" href="Id.html#1249" class="Function Operator">⁻¹</a> <a id="2400" href="Id.html#1743" class="Function Operator">⟩</a>
  <a id="2404" class="Symbol">(</a><a id="2405" href="NaturalsAddition.html#2236" class="Bound">x</a> <a id="2407" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="2409" href="NaturalsAddition.html#2240" class="Bound">z</a><a id="2410" class="Symbol">)</a> <a id="2412" href="NaturalsAddition.html#207" class="Function Operator">+</a> <a id="2414" href="NaturalsAddition.html#2238" class="Bound">y</a> <a id="2416" href="Id.html#1825" class="Function Operator">∎</a>


<a id="2420" class="Markup">\end{code}</a><a id="2430" class="Background">
</a></pre></body></html>